plugins {
    id 'application'
    id 'java'
    id 'java-library'

    id 'maven-publish'

    id 'eclipse'

    id "com.adarshr.test-logger" version '3.2.0'
}

group = 'com.qaware.mcp'
version = '1.1.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // mcp
    implementation 'io.modelcontextprotocol.sdk:mcp:0.17.0'
    implementation 'org.eclipse.jetty:jetty-server:12.0.22'
    implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.0.22'

    // json
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

    // logging
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'

    // hppc
    implementation 'com.carrotsearch:hppc:0.10.0'

    // tika
    implementation 'org.apache.tika:tika-core:3.2.3'
    implementation 'org.apache.tika:tika-parsers-standard-package:3.2.3'

    // junit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.12.2'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher:1.12.2'
    
    
    
    
    
    implementation 'com.google.code.gson:gson:2.13.2' // XXX
    
    
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'com.qaware.mcp.Server'
}

tasks.named('run', JavaExec) {
    classpath += files(".")
}

jar {
    archiveBaseName = 'mcp-server-kickstart'
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = "Assembles a fat jar archive containing the classes of the 'main' feature and all dependencies."

    archiveBaseName = 'mcp-server-kickstart-all'
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.SF',
                'META-INF/*.DSA',
                'META-INF/*.RSA',
                'META-INF/*.EC',
                'META-INF/SIG-*'
    }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

clean {
    delete file('bin')
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
